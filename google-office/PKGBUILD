# Maintainer: Robert Fry <contact@robertfry.xyz>

pkgname=nfc-google-office
pkgver=1
pkgrel=1
pkgdesc="Nativefier Collections; Google Office (drive.google.com)"
arch=(x86_64)
url=https://drive.google.com
license=('GPL','custom:2')
makedepends=(nodejs-nativefier)
source=(nfc-google-office.desktop
        nfc-google-office.png)
sha256sums=(e98c5178433c092c79937e0c40940311da7c588d07c4fef28d8f371b8330c6a4
            66bdce2fa39ca142d63f322cf9b0dc93873269930652a30037c1882fe99e02fd)

_instname=nfc-google-office
_execname=NativefierCollections-GoogleOffice
_builddir=$_execname-linux-x64

build() {
  nativefier "https://drive.google.com/" \
    --name $_execname \
    --icon "nfc-google-office.png" \
    --user-agent "Mozilla/5.0 (X11; Ubuntu; Linux x86_64; rv:103.0) Gecko/20100101 Firefox/103.0" \
    --internal-urls ".*?(accounts|drive|docs|sheets|slides)\.google\.com.*?" \
    --single-instance --tray
}

package() {
  install -d "$pkgdir"/opt
  cp -rL $_builddir "$pkgdir"/opt/$pkgname

  install -d "$pkgdir"/usr/{bin,share/pixmaps}
  ln -sf /opt/$pkgname/$_execname "$pkgdir"/usr/bin/$_instname
  ln -sf /opt/$pkgname/resources/app/icon.png "$pkgdir"/usr/share/pixmaps/$_instname.png

  install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$_instname.desktop
}
