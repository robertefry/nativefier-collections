# Maintainer: Robert Fry <contact@robertfry.xyz>

pkgname=nfc-protonmail
pkgver=1
pkgrel=1
pkgdesc="Nativefier Collections; ProtonMail (protonmail.com)"
arch=(x86_64)
url=https://mail.protonmail.com/
license=('GPL','custom:2')
makedepends=(nodejs-nativefier)
source=(nfc-protonmail.desktop
        nfc-protonmail.png)
sha256sums=(d8d26a02a4ebe58571d4b94409327657c403fc7ed89614cfe02b264994eb06e4
            a100b72cc057d7cadc3e66ca8995920d6d38f64adbd1b0f0498dbef507453e7e)

_instname=nfc-protonmail
_execname=NativefierCollections-ProtonMail
_builddir=$_execname-linux-x64

build() {
  nativefier "https://mail.protonmail.com/" \
    --name $_execname \
    --icon "nfc-protonmail.png" \
    --internal-urls "proton\.me"
}

package() {
  install -d "$pkgdir"/opt
  cp -rL $_builddir "$pkgdir"/opt/$pkgname

  install -d "$pkgdir"/usr/{bin,share/pixmaps}
  ln -sf /opt/$pkgname/$_execname "$pkgdir"/usr/bin/$_instname
  ln -sf /opt/$pkgname/resources/app/icon.png "$pkgdir"/usr/share/pixmaps/$_instname.png

  install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$_instname.desktop
}
