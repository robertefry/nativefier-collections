# Maintainer: Robert Fry <contact@robertfry.xyz>

pkgname=nfc-google-translate
pkgver=1
pkgrel=1
pkgdesc="Nativefier Collections; Google Translate (translate.google.com)"
arch=(x86_64)
url=https://translate.google.com
license=('GPL','custom:2')
makedepends=(nodejs-nativefier)
source=(nfc-google-translate.desktop
        nfc-google-translate.png)
sha256sums=(f8948ec9f331dc0bc3254dc6f38d312c7e24f2e89a6082c48cc42099db757154
            67983ca91db1b75c9aa67a98affe3e2cca79509fd78e87e77d85158c8bd5c7ee)

_instname=nfc-google-translate
_execname=NativefierCollections-GoogleTranslate
_builddir=$_execname-linux-x64

build() {
  nativefier "https://translate.google.com/" \
    --name $_execname \
    --icon "nfc-google-translate.png" \
    --user-agent "Mozilla/5.0 (X11; Ubuntu; Linux x86_64; rv:103.0) Gecko/20100101 Firefox/103.0" \
    --internal-urls ".*?www\.google\.com.*?"
}

package() {
  install -d "$pkgdir"/opt
  cp -rL $_builddir "$pkgdir"/opt/$pkgname

  install -d "$pkgdir"/usr/{bin,share/pixmaps}
  ln -sf /opt/$pkgname/$_execname "$pkgdir"/usr/bin/$_instname
  ln -sf /opt/$pkgname/resources/app/icon.png "$pkgdir"/usr/share/pixmaps/$_instname.png

  install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$_instname.desktop
}
